plugins {
    id 'fabric-loom' version '1.11.2'
    id 'maven-publish'
    id("org.jetbrains.kotlin.jvm") version "2.2.0"
}

version = project.mod_version
group = project.maven_group

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation"
        }
    }
}

base {
    archivesName = project.archives_base_name
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "novaraids" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url = "https://maven.impactdev.net/repository/development/"
    }
    maven {
        url = "https://maven.nucleoid.xyz"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation include("net.fabricmc.fabric-api:fabric-data-attachment-api-v1:1.4.1+9aea556b2d")

    modImplementation include("club.minnced:discord-webhooks:${project.discord_webhooks_version}")

    include("com.squareup.okhttp3:okhttp:${project.okhttp_version}")
    include("com.squareup.okio:okio:${project.okio_version}")
    include("com.squareup.okio:okio-jvm:${project.okio_jvm_version}")
    include("org.json:json:${project.json_version}")

    modImplementation include('me.lucko:fabric-permissions-api:0.3.1')

    modImplementation include("net.kyori:adventure-text-minimessage:4.17.0")
    modImplementation include("net.kyori:adventure-platform-fabric:5.14.1")

    modImplementation include("eu.pb4:sgui:1.6.1+1.21.1")

    implementation include("com.cronutils:cron-utils:9.2.1")

    modImplementation "com.cobblemon:fabric:${project.cobblemon_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    inputs.property "cobblemon_version", project.cobblemon_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version,
                "cobblemon_version": project.cobblemon_version
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    repositories {
    }
}
